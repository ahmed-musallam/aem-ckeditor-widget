buildscript {
    apply from: "$rootProject.projectDir/gradle/buildscript.gradle", to: buildscript
}

description = "CKEditor Granite Widget"
defaultTasks = ['aemRootDeploy']

apply plugin: 'com.cognifide.aem'

allprojects { subproject ->
    group = 'com.ckeditor.aem'
    version = '1.0.0-SNAPSHOT'

    apply plugin: "eclipse"
    apply plugin: "idea"

    repositories {
        maven { url "https://repo.adobe.com/nexus/content/groups/public" }
        maven { url "https://repo1.maven.org/maven2" }
        jcenter()
        mavenCentral()
        mavenLocal()
    }

    plugins.withId 'com.cognifide.aem', {

        // Common AEM configuration (CRX packages, deployment etc)

        aem {
            config {
                contentPath = subproject.file("src/main/content")
                instance("http://localhost:4502", "admin", "admin", "local-author")
                // instance("http://localhost:4503", "admin", "admin", "local-publish")
            }
        }

    }

    plugins.withId 'java', {

        sourceCompatibility = 1.8
        targetCompatibility = 1.8

        def compileOptions = {
            options.encoding = "UTF-8"
            options.compilerArgs << '-Xlint:deprecation'
        }

        compileJava compileOptions
        compileTestJava compileOptions

    }

    plugins.withId 'org.dm.bundle', {

        // AEM runtime dependencies

        dependencies {
            testCompile group: 'junit', name: 'junit', version:'4.8.1'

            compile group: 'org.osgi', name: 'org.osgi.annotation', version: '6.0.0'
            compile group: 'org.osgi', name: 'org.osgi.service.component.annotations', version: '1.3.0'
            compile group: 'org.osgi', name: 'org.osgi.service.metatype.annotations', version: '1.3.0'
            compile group: 'javax.servlet', name: 'servlet-api', version:'2.5'
            compile group: 'javax.jcr', name: 'jcr', version:'2.0'
            compile group: 'org.slf4j', name: 'slf4j-api', version:'1.5.10'
            compile group: 'org.apache.geronimo.specs', name: 'geronimo-atinject_1.0_spec', version: '1.0'
            compile group: 'org.apache.sling', name: 'org.apache.sling.api', version:'2.2.0'
            compile group: 'org.apache.sling', name: 'org.apache.sling.jcr.api', version:'2.1.0'
            compile group: 'org.apache.sling', name: 'org.apache.sling.models.api', version: '1.1.0'
            compile group: 'com.google.code.gson', name: 'gson', version: '2.3'
            compile group: 'com.adobe.aem', name: 'uber-jar', version: '6.2.0', classifier: 'obfuscated-apis'
            compile group: 'joda-time', name: 'joda-time', version: '2.9.1'
        }

    }
}

aemCompose {
    includeProjects ':app:*'
}

aemSatisfy {
    group 'dependencies', {
        // local("pkg/vanityurls-components-1.0.2.zip")
    }

    group 'tools', {
       // download('https://github.com/OlsonDigital/aem-groovy-console/releases/download/9.0.1/aem-groovy-console-9.0.1.zip')
       // download("https://github.com/Cognifide/APM/releases/download/cqsm-2.0.0/apm-2.0.0.zip")
    }
}